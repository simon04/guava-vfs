plugins {
    id 'com.jfrog.bintray' version '1.3.1'
}

apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group = 'net.simon04'
version = project.property('currentVersion')

compileJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.guava:guava:16+'
    compile 'org.apache.commons:commons-vfs2:2.+'
    testRuntime 'org.slf4j:jcl-over-slf4j:1+'
    testCompile 'junit:junit:4.12'
    testCompile 'com.google.truth:truth:0.27'
}

javadoc {
    options {
        links 'http://docs.oracle.com/javase/7/docs/api/'
        links 'http://docs.guava-libraries.googlecode.com/git/javadoc/'
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'guava-vfs'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/simon04/guava-vfs'
        vcsUrl = 'https://github.com/simon04/guava-vfs.git'
        issueTrackerUrl = 'https://github.com/simon04/guava-vfs/issues'
        version {
            name = project.property('currentVersion')
            released = new Date()
        }
    }
}
